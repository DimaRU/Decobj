.INCLUDE /DECDFN/
.MODULE	DEGDBG,RELEASE=V08,VERSION=00,COMMENT=<DECOBJ DEBUGGING>

.MCALL	.TRPSET	.PRINT

DBGSET::
	.TRPSET	#AREA,#TRPINT
	RETURN

TRPINT:
	MOV	#" 4,VEC
	BCC	1$
	MOV	#"10,VEC
1$:	MOV	R0,VECSAV
	MOV	#VECSAV+2,R0
	MOV	R1,(R0)+
	MOV	R2,(R0)+
	MOV	R3,(R0)+
	MOV	R4,(R0)+
	MOV	R5,(R0)+
	TST	(R0)+		; SP
	MOV	(SP)+,(R0)+	; PC
	MOV	(SP)+,(R0)+	; PSW
	MOV	SP,-6(R0)
	MOV	(SP)+,(R0)+
	MOV	(SP)+,(R0)+

	MOV	#VECSAV,R5
	MOV	#MSGTBL,R4
	.PRINT	#MSG1
	MOV	#11.,R1
3$:	.PRINT	R4
	MOV	(R5)+,R0
	CALL	FOCONV
	.PRINT	#CVTBUF
2$:	TSTB	(R4)+
	BPL	2$
	DEC	R1
	BNE	3$
	MOV	DECSP,SP
STOP==.
	NOP
	JMP	DECOBJ

AREA:	.BLKW	10
VECSAV:	.BLKW	20.
MSG1:	.ASCII	/ПРЕРЫВАНИЕ ПО ВЕКТОРУ /
;		 123456789 123456788 12
VEC:	.ASCIZ	/  /	
MSGTBL:	.ASCII	/R0=/<200>
	.ASCII	/R1=/<200>
	.ASCII	/R2=/<200>
	.ASCII	/R3=/<200>
	.ASCII	/R4=/<200>
	.ASCII	/R5=/<200>
	.ASCII	/SP=/<200>
	.ASCII	/PC=/<200>
	.ASCII	/PSW=/<200>
	.ASCII	/ROUT1=/<200>
	.ASCII	/ROUT2=/<200>
	.EVEN
	.END
